1 REM DIE ROLL
2 REM HARDWARE: Stephen Cousins' SC103 Z80 PIO Module at port 0x68
3 REM CONNECTIONS: J2 Pins 1-4 = PIO Pins PA0-PA3
4 REM              J2 Pin 8  = PIO Pin PA7
5 REM              J2 Pin 9  = PIO Pin PA4
6 REM              J2 PIN 10 = PIO GND
7 REM              J1 Pin 1  = PIO 5v
8 REM              J1 Pin 2  = PIO GND
10 OUT 106, 207  : REM set PIO A to control mode
12 OUT 106, 128  : REM 0-4 output, 7 input
15 PRINT "LET 'EM ROLL"
20 IF INP(104) > 128 THEN D=(INT((RND(1)*(7.0-1.0))) + 1) : GoTo 20
30 FOR I = 1 TO 30
40 D=(INT((RND(1)*(7.0-1.0))) + 1) : GOSUB 100
50 NEXT I
60 PRINT "ROLL=",D:GoTo 20
99 REM DISPLAY DIE VALUE
100 IF D=1 THEN OUT 104, 1 : OUT 104, 17 : GoTo 180
110 IF D=2 THEN OUT 104, 2 : OUT 104, 18 : GoTo 180
120 IF D=3 THEN OUT 104, 3 : OUT 104, 19 : GoTo 180
130 IF D=4 THEN OUT 104, 6 : OUT 104, 22 : GoTo 180
140 IF D=5 THEN OUT 104, 7 : OUT 104, 23 : GoTo 180
150 IF D=6 THEN OUT 104, 14 : OUT 104, 30
180 RETURN
200 END
