10 REM DEMO PROGRAM FOR QUAZAR'S SID INTERFACE
15 REM ON A RC2014 RUNNING ROMWBW 3.0.1

20 REM == POKE IN ASSEMBLY AT 0xF800 ==
30 LET MB=&HF800
100 PRINT "Poking in the assembly...";
110 READ OP
120 IF OP = 999 THEN GOTO 160
130 POKE MB, OP
140 LET MB = MB + 1
150 GOTO 110
160 PRINT "...Done!"

200 REM == JP to CLEAR-SID (c3 00 f8 = jp f816) ==
210 MB = &H2293
220 POKE MB, &HC3
230 POKE MB+1, &H16
240 POKE MB+2, &HF8
250 PRINT "Calling CLEAR-SID()...";
260 R = USR(0)
270 PRINT "Done!"

300 REM == SETUP USRLOC to call SID-IO-BAS at 0xF800 == 
310 POKE MB+1, &H00
315 LET S = 54272 : REM THIS IS JUST FOR THE C64
320 LET I = 0 : REM TURN OFF INTERRUPTS

415 A = 2^(1/12) : REM CONSTANT FOR FREQUENCY FORMULA
420 F0 = 7454: REM A4 USED AS REFERENCE NOTE  (440 HZ = SID VALUE 7454)
425 N=-9 : REM WE WANT TO PLAY C4 NOTE, WHICH IS -9 HALFTONES AWAY FROM A4
430 NF = INT(F0 * A^N) : REM FREQUENCY OF THE NOTE TO BE PLAYED
435 FH=INT(NF/256) : FL=NF-256*FH : REM HIGHBYTE AND LOW BYTE OF FREQUENCY
440 R = 0 : D = FL : GOSUB 1000 : REM SETS FREQUENCY OF NOTE FOR VOICE 1
441 R = 1 : D = FH : GOSUB 1000 : REM SETS FREQUENCY OF NOTE FOR VOICE 1
445 WF=32 : REM SAWTOOTH WAVEFORM FOR VOICE 1
450 R = 5 : D = 13*16+5 : GOSUB 1000 : REM ATTACK = 5: DECAY = 3
455 R = 6 : D = 12*16+10 : GOSUB 1000 : REM SUSTAIN = 15: RELEASE = 10
460 R = 24 : D = 15 : GOSUB 1000 : REM GLOBAL VOLUME SET TO FULL LEVEL
465 DR = 2000 : REM DURATION OF NOTE
470 R = 4 : D = WF+1 : GOSUB 1000 : REM SETS GATE BIT = STARTS NOTE
475 FOR T = 1 TO DR: NEXT : REM KEEPS PIANO KEY DEPRESSED FOR DURATION OF NOTE
480 R = 4 : D = WF+0 : GOSUB 1000 : REM TURNS OFF GATE BIT, PIANO KEY RELEASED
490 END

1000 REM PRINT "POKE S+";R;",";D
1005 IF D > 127 THEN D = D - 256
1007 LET ARG = (D * 256) + R + I
1008 REM PRINT "ARG=";ARG
1010 RESULT = USR(ARG)
1015 REM PRINT "USR=";RESULT
1020 RETURN

9000 REM == SID-IO-BAS 0xF800 ==
9001 DATA 205, 241, 11, 123
9010 REM == SID-IO 0xF804 ==
9011 DATA 14,84,71,237,81,0,0,0,203,248,237,81
9012 DATA 66,62,0,195,103,19
9020 REM == CLEAR-SID 0xF816 ==
9021 DATA 14,84,6,152,175,237,121,0,0,0,203,184,237
9022 DATA 121,0,0,0,203,248,237,121,201
9999 DATA 999
