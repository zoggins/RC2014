	SUBTTL	Error Routines
;
CLERR	XOR	A		;CLEAR ERROR LIST
	LD	($EC),A
	LD	($EC+1),A
	PUSH	HL
	LD	HL,ERLIST
	LD	($ERP),HL
	POP	HL
	RET
;
ERRORS	CALL	PUSHEM
	LD	HL,($EC)	;CHECK FOR ERRORS
	LD	A,H
	OR	L
	JR	NZ,ERR00
	LD	HL,NERR		;NONE HAVE OCCURRED
	LD	(PENDM),HL	;SO PRINT No Errors
	CALL	POPEM		;WHEN RETURNING
	AND	A
	RET
ERR00	LD	HL,ERLIST
ERR0	CALL	CLS
	LD	A,CR
	CALL	CRT
	LD	B,22		;LINES PER SCREEN
ERR1	PUSH	BC
	LD	DE,($ERP)	;CHECK FOR END OF LIST
	CALL	CPHLDE
	JR	NC,ERR2
	CALL	PRERR		;PRINT ERROR
	POP	BC
	DJNZ	ERR1		;DO NEXT
	PSCR	KPRESS
	CALL	GBYTE		;WAIT FOR KEY INPUT
	CP	ESC
	JR	Z,ERR3		;ABORT IF ESC
	JR	ERR0		;DO NEXT SCREEN
ERR2	POP	BC		;TIDY STACK
	PSCR	KPRESS		;WAIT
	CALL	GBYTE
ERR3	CALL	POPEM		;RESTORE REGS
	SCF
	RET
;
PRERR	LD	A,(HL)
	INC	HL
	BIT	7,A		;FIND OUT IF
	JR	Z,PRE1		;A->B OR B->A
	LD	DE,TBA
	JR	PRE2
PRE1	LD	DE,TAB
PRE2	AND	7FH
	CP	'S'		;FIND OUT IF
	JR	Z,PRE3		;SERIAL OR
	CP	'P'		;PARALLEL
	JR	NZ,PRE5		;OR SPURIOUS
	PMESS	PERR
	JR	PRE4
PRE3	PMESS	SERR
PRE4	EX	DE,HL
	CALL	PSTRING
	EX	DE,HL
	PMESS	MSERR
	LD	A,(HL)
	INC	HL
	CALL	HEX2
	PMESS	MRERR
	LD	A,(HL)
	INC	HL
	CALL	HEX2
PRE4A	LD	A,CR
	JP	CRT
PRE5	PMESS	SPERR
	CALL	HEX
	INC	HL
	INC	HL
	JR	PRE4A
;
LOGERR	CALL	PUSHEM		;ROUTINE TO LOG ERRORS
	LD	HL,($ERP)	;GET POINTER
	PUSH	AF		;A=CHAR OUT
	LD	DE,ERLISTE
	CALL	CPHLDE		;LIST FULL?
	JR	NC,ABORT	;YES->
	LD	A,(ERRTYPE)	;GET TEST TYPE
	LD	(HL),A		;SAVE IN LIST
	INC	HL		;BUMP PTR
	POP	AF		;RETRIEVE CHAR
	LD	(HL),A		;SAVE IN LIST
	INC	HL
	LD	(HL),B		;B=CHAR IN
	INC	HL
	LD	($ERP),HL	;SAVE PTR
	LD	HL,($EC)
	INC	HL
	LD	($EC),HL
	CALL	POPEM		;RESTORE REGS
	OR	A		;CLEAR CARRY
	RET
;
ABORT	POP	AF		;TIDY STACK
	LD	HL,ABM		;ABORT MESS
	LD	(PENDM),HL
	CALL	POPEM		;RESTORE REGS
	SCF			;SET CARRY
	RET			;TO INDICATE ERROR!
;
SERR	DM	'Serial  '
PERR	DM	'Parallel'
TAB	DM	' A->B '
TBA	DM	' B->A '
MSERR	DM	' Test, Byte sent = '
MRERR	DM	', Byte received = '
KPRESS	DM	24,1,'Press any key to continue '
SPERR	DM	'Spurious Error - Flag = '
NERR	DM	20,3,'No Errors'
;
ERRTYPE	DS	1		;ERROR TYPE
$EC	DW	0		;ERROR COUNT
$ERP	DW	ERLIST		;ERROR LIST ADDRESS
$ERLSTE	DW	ERLISTE-ERLIST	;ERROR LIST SPACE
