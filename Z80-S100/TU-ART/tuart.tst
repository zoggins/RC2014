	SUBTTL Main Program
;
;	****************
;	* MAIN PROGRAM *
;	****************
;
START	LD	HL,(6)
	LD	SP,HL
	LD	HL,START
	PUSH	HL
	LD	A,(CURPSN)
	OR	A
	JR	NZ,MP1
	CALL	TERMNL
MP1	CALL	CLS
	CALL	INITVSPC
	PSCR	MENU
	PSCR	(PENDM)
	LD	HL,PARK
	LD	(PENDM),HL
	CALL	GBYTE
	CP	'A'
	JP	Z,AUTO
	CP	'C'
	JP	Z,CNTRS
	CP	'E'
	JP	Z,ERRORS
	CP	'P'
	JP	Z,PTEST
	CP	'S'
	JP	Z,STEST
	CP	'T'
	JP	Z,SETUP
	CP	'X'
	JP	Z,0
	LD	A,7
	JP	CRT
;
	SUBTTL Test Routines
;
;	*****************
;	* TEST ROUTINES *
;	*****************
;
AUTO	CALL	CCRSR
AUTO0	CALL	CLS
	PSCR	AUTOTST
AUTO1	CALL	CTALL1
	JR	C,AUTO2
	CALL	CLERR
	PSCR	STESTM
	CALL	STESTA
	JR	C,AUTO0
	CALL	CHKIN
	LD	A,(ICHAR)
	CP	ESC
	JR	Z,AUTO2
	CALL	DELAY
	JR	AUTO1
AUTO2	CALL	CCRSR
	RET
;
AUTOTST	DB	3,3,'Automatic Tests being performed.'
	DM	' <Esc> to exit.',CR,'  '
;
STESTM	DM	12,3,'Serial Test     '
;
CNTRS	LD	HL,CMTYPE
	LD	(TYPEM),HL
	CALL	AORS
	RET	C
	LD	(AUTOFLG),A
	CP	'A'
	JP	Z,CTALL
CTSNGL	CLL	7
	PSCR	CMDEV
	CALL	GBYTE
	CP	ESC
	JR	Z,CNTRS
	CP	CR
	JR	Z,CDA
	CP	'A'
	JR	Z,CDA
	CP	'B'
	JR	Z,CDB
	LD	A,7
	CALL	CRT
	JR	CTSNGL
CDA	LD	A,($TDA)	;DEVICE A
	JR	CD1
CDB	LD	A,($TDB)	;DEVICE B
CD1	LD	(CDEV),A	;PUT PORT ADDR
	LD	A,(ICHAR)	;IN CDEV AND ECHO
	CALL	CRT		;THE INPUT
CD2	CLL	8
	PSCR	CMCNTR
	LD	A,1
	CALL	GNUM		;GET COUNTER NO
	JR	C,CTSNGL
	OR	A
	JR	Z,CD2
	CP	6
	JR	NC,CD2
	LD	(CCNTR),A
CD3	CLL	9
	PSCR	CMCNT
	LD	A,0FFH
	CALL	GNUM		;GET COUNT
	JR	C,CD2
	LD	(CDATA),A
	CALL	CCRSR
	CALL	LOADT		;LOAD AND TEST
	CLL	12
	PSCR	DMESS
	PSCR	($INTMSG)
	CALL	CCRSR
	CALL	CHKERR
	JR	CD3
;
CTALL	CALL	CCRSR
	CALL	CTALL1
	CALL	CCRSR
	JP	CNTRS
;
CTALL1	LD	A,0FFH
	LD	(CDATA),A
	LD	A,($TDA)
CTA0	LD	(CDEV),A
	XOR	A
CTA1	INC	A
	CP	6
	JR	Z,CTA2
	PUSH	AF
	LD	(CCNTR),A
	CALL	LOADT
	LD	A,(ICHAR)
	CP	ESC
	JR	Z,CTA4
	CLL	12
	PSCR	DMESS
	PSCR	($INTMSG)
	CALL	CHKERR
	CALL	DELAY
	POP	AF
	JR	CTA1
CTA2	LD	A,($TDB)
	LD	HL,CDEV
	CP	(HL)
	JR	NZ,CTA0
	AND	A
	RET
CTA4	POP	AF
	SCF
	RET
;
CHKERR	LD	HL,($INTMSG)
	LD	DE,MIERR
	CALL	CPHLDE
	JP	Z,ERRFLG
	RET
ERRFLG	PMESS	FLGM
	LD	HL,(INTADDR)
	CALL	HEX4
	RET
;
FLGM	DM	', Address = '
;
LOADT	LD	A,(CDEV)	;GET CURRENT DEV
	ADD	COMM		;COMMAND PORT
	LD	C,A
	LD	A,9
	OUT	(C),A		;RESET AND ENABLE INTA.
	INC	C
	LD	A,(CCNTR)	;GET COUNTER
	PUSH	AF
	LD	E,A
	LD	D,0
	DEC	E
	LD	HL,TTAB
	ADD	HL,DE		;COMPUTE MASK
	LD	A,(HL)		;AND OUTPUT TO
	OUT	(C),A		;INTERRUPT REG.
	POP	AF
	PUSH	BC
	INC	A
	ADD	C
	LD	C,A
	LD	A,(CDATA)	;GET DATA AND
	OUT	(C),A		;OUTPUT TO PORT.
	CALL	WAIT		;WAIT FOR INT'T.
	POP	BC
	XOR	A
	OUT	(C),A		;DISABLE INT
	RET
;
CDEV	DS	1		;CURRENT DEVICE
CCNTR	DS	1		;CURRENT COUNTER
CDATA	DS	1		;CURRENT DATA
;
CMTYPE	DM	3,1,'COUNTER TEST'
SMTYPE	DM	3,1,'SERIAL TEST'
;
Q1	DB	4,3,'A - Automatic',CR
	DM	'  S - Single test'
;
CMDEV	DM	7,5,'Device  [A] ? '
CMCNTR	DM	8,5,'Counter [1] ? '
CMCNT	DM	9,5,'Count  [FF] ? '
;
;	SERIAL TEST ROUTINE
;
STEST	CALL	CLS
	PSCR	SMTYPE
STST1	CALL	CLERR
	CALL	STESTA
	LD	A,(ICHAR)
	CP	ESC
	RET	Z
	JR	STST1
;
STESTA	LD	B,2		;DO TWICE
	XOR	A		;A->B THEN B->A
	LD	(ICHAR),A
	LD	A,'S'
	LD	(ERRTYPE),A	;FOR SERIAL
ST11	PUSH	BC
	XOR	A
	LD	(INTFLG),A
	LD	(IFLG),A
	LD	(BYTE),A
	LD	(BYTE2),A
	LD	HL,$TDA
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	OUT	(C),A		;SET BAUD RATE
	LD	A,9
	INC	C
	INC	C
	OUT	(C),A
	LD	A,20H
	INC	C
	OUT	(C),A		;TBE ENABLE
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	OUT	(C),A
	LD	A,9
	INC	C
	INC	C
	OUT	(C),A
	LD	A,10H		;RDA ENABLE
	INC	C
	OUT	(C),A
	IM2
	EI			;INTERRUPTS ON.
ST1	CALL	CHKIN		;CHECK INPUT FOR
	LD	A,(ICHAR)	;ESCAPE.
	CP	ESC
	JR	Z,STESC
	LD	A,(INTFLG)	;CHECK FOR END
	OR	A		;OF TEST.
	JR	Z,ST1
	CP	2
	JR	NZ,STESC
	DI
	PSCR	MIERR
	CALL	ERRFLG
	PSCR	KPRESS
	CALL	GBYTE
	CLL	24
STESC	DI
	CALL	SWAPAB		;SWAP ADDRESSES
	POP	BC
	LD	A,'S'+80H	;B->A SERIAL TEST
	LD	(ERRTYPE),A
	DJNZ	ST11		;DO AGAIN B->A
	JP	ERRORS		;PRINT ERRORS
;
BYTE	DS	1
BYTE2	DS	1
;
PTEST
	PSCR	NYA
	JP	DELAY
;
SWAPAB	LD	HL,($TDA)
	LD	(TEMP1),HL	;SWAP DEV A AND B
	LD	HL,($TDB)	;ADDRESSES FOR
	LD	($TDA),HL	;SECOND (LAST)
	LD	HL,(TEMP1)	;ITERATION
	LD	($TDB),HL
	RET
;
;
SETUP	CALL	CLS
	PSCR	CEH
	PSCR	TPM1
	LD	A,20H
	CALL	GNUM
	RET	C
	AND	0F0H
	LD	(TEMP1),A
S1	PSCR	TPM2
	LD	A,50H
	CALL	GNUM
	JR	C,SETUP
	AND	0F0H
	LD	($TDB),A
	LD	A,(TEMP1)
	LD	($TDA),A
	JP	INITVSPC
;
$TDA	DB	20H		;TUART PORT A BASE ADDR
BDRA	DB	40H		;BAUD RATE A
$TDB	DB	50H		;TUART PORT B BASE ADDR
BDRB	DB	40H		;BAUD RATE B
;
TPM1	DB	3,3,' Please note switches 1 and 2 on the tuart must be off.'
	DB	CR,CR
	DM	'  Port A Address [20] ?'
TPM2	DM	6,3,'Port B Address [50] ?'
CEH	DM	22,3,'Type in data, <CR> for default or <ESC> to go back.'
;
TYPEM	DS	2		;ADDRESS OF TEST MESSAGE
;
AORS	CALL	CLS
	PSCR	(TYPEM)
	PSCR	Q1
	PSCR	CEH
	CRSR	6,3
AS1	CALL	GBYTE
	CP	ESC
	JR	Z,ASESC
	CP	'A'		;ALL TESTS
	RET	Z
	CP	'S'		;SINGLE TEST
	RET	Z
	LD	A,7
	CALL	CRT
	JR	AS1
ASESC	SCF
	RET
;
DELAY	PUSH	HL
	LD	HL,0FFFFH
D1	LD	A,H
	OR	A,L
	JR	Z,D2
	PUSH	HL
	POP	HL
	DEC	HL
	JR	D1
D2	POP	HL
	RET
;
TERMNL	PMESS	TERMESS
	CALL	GBYTE
	CP	'1'
	JR	Z,C3102
	CP	'2'
	JR	NZ,TERMNL
	LD	A,'='
	LD	(CURPSN),A
	LD	A,1AH		;CLEAR SCREEN
	LD	(CLESC),A
	RET
C3102	LD	A,'F'
	LD	(CURPSN),A
	LD	A,ESC
	LD	(CLESC),A
	LD	A,'E'
	LD	(CLESC2),A
	RET
;
CLESC	DB	0		;CLEAR CHAR
CLESC2	DB	0
CURPSN	DB	0		;ESCAPE CHAR FOR CRT
;
TERMESS	DB	CR,' 1 - Cromemco 3102 Terminal',CR
	DM	' 2 - Lear Seigler ADM 3A+',CR,'?'
;
BLL	DB	'                                        '
	DM	'                                       '
;
