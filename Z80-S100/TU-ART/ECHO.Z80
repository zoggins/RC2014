;
;
;		TUART PROGRAMMING EXAMPLES
;
;
;
ABASE:	EQU		50H
BBASE:	EQU		20H
;
BAUDA:	EQU 	0C0H
BAUDB:	EQU		1
ABDR:	EQU		ABASE
BBDR:	EQU		BBASE
;
RESET:	EQU		9
ACMD:	EQU		ABASE+2
BCMD:	EQU 	BBASE+2
;
MASKA:	EQU	0
MASKB:	EQU 0
AMSK	EQU	ABASE+3
BMSK	EQU	BBASE+3
;
;		EXAMPLE 1 -- INITIALIZATION ROUTINE
;
;		SUBROUTINE INIT:
;				CALLING PARAMETERS: NONE
;				RETURN CONDITION: TUART INIT'ED
;
;
;				
		ORG		1000H
INIT:	PUSH	AF		;SAVE STATE
		LD		A,RESET	;GET COMMAND
		OUT		ACMD,A	;DEVICE A RESET
		OUT		BCMD,A	;DEVICE B RESET
;
		LD		A,MASKA	;GET INTERRUPT MASK
		OUT		AMSK,A	;MASK A SET
		LD		A,MASKB	;GET INTERRUPT MASK
		OUT		BMSK,A	;MASK B SET
;
		LD		A,BAUDA	;GET BAUD RATE
		OUT		ABDR,A	;RATE A SET
		LD		A,BAUDB	;GET BUAD RATE
		OUT		BBDR,A	;RATE B SET
		POP		AF		;RETRIEVE STATE
		RET
		
;
;		EXAMPLE 2 -- SUBROUTINE TO TRANSMIT A CHARACTER
;					 TO DEVICE A
;
;		SUBROUTINE CHAROUT:
;				CALLING PARAMETERS: ASCII CHR. IN REG A
;				RETURN CONDITION: CHARACTER SENT
;
;
ASTAT:	EQU		ABASE+0	;STATUS REGISTER A
ADATA:	EQU		ABASE+1	;DATA REGISTER A
TBE:	EQU		80H		;BUFFER EMPTY BIT
;
CHROUT:	PUSH	AF		;SAVE THE CHARACTER
SCHK:	IN		A,ASTAT	;READ STATUS
		AND		TBE		;TRANSMIT BFR. EMPTY?
		JR		Z,SCHK	;LOOP UNTIL REAQDY
		POP		AF		;RETRIEVE CHARACTER
		OUT		ADATA,A	;TRANSMIT IT
		RET				;DONE
		
;
;		EXAMPLE 3 -- SUBROUTINE TO READ A CHARACTER
;					 FROM DEVICE A
;
;		SUBROUTINE CHRIN:
;				CALLING PARAMETERS: NONE
;				RETURN CONDITION: ASCII CHR IN A
;								  Z FLAG RESET
;
;
RDA:	EQU		40H		;DATA AVALIABLE BIT
CHRIN:	IN		A,ASTAT	;GET STATUS
		AND		RDA		;RCVR DATA AVAIL?
		JR		Z,CHRIN	;LOOP TILL READY
		IN		A,ADATA	;READ CHARACTER
		RET				;DONE

;		
;		EXAMPLE 4 -- "ECHO" PROGRAM FOR DEVICE A
;
;
		ORG 	100H
STACK:	EQU		200H	;STACK AREA
MAIN:	LD		SP,STACK;SET STACK POINTER
		CALL	INIT	;RESET TUART
READ:	CALL	CHRIN	;WAIT FOR AN INPUT
		CALL	CHROUT	;NOW SEND IT BACK
		JR		READ	;LOOP
;
		END		MAIN
		