0 REM      R O C K E T   L A N D E R 
2 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
5 OUT 2,26
10 P=1
11 REM ....P  IS THE INPUT PORT NUMBER FOR YOUR KEYBOARD......
15 REM
20 REM
70 PRINT "LUNER LANDING SIMULATIONS"
80 PRINT "----- ------- -----------"
100 PRINT:PRINT "DO YOU WANT INSTRUCTIONS"
110 INPUT "(Type 0 for NO)";A
120 IF A=0 THEN 390
160 PRINT
200 PRINT"YOU ARE LANDING ON THE MOON AND HAVE TAKEN OVER MANUAL"
210 PRINT"CONTROL 500 FEET ABOVE A GOOD LANDING SPOT. YOU HAVE A"
220 PRINT"DOWNWARD VELOCITY OF 50 FT/SEC.   120 UNITS OF FUEL REMAIN."
225 PRINT
230 PRINT"HERE ARE THE RULES THAT GOVERN YOUR SPACE VEHICLE:"
240 PRINT"(1) AFTER EACH SECOND, THE HEIGHT, VELOCITY, AND REMAINING"
250 PRINT"    FUEL WILL BE REPORTED."
260 PRINT"(2) AFTER THE REPORT, A'?'WILL BE TYPED.  ENTER THE"
270 PRINT"    NUMBER OF UNITS OF FUEL YOU WISH TO BURN DURING THE "
280 PRINT"    NEXT SECOND.  EACH UNIT OF FUEL WILL SLOW YOUR DESCENT"
290 PRINT"(3) THE MAXIMUM THRUSTOF YOUR ENGINE IS 30 FT/SEC/SEC OR"
300 PRINT"    30 UNITS OF FUEL PER SECOND"
310 PRINT"(4) WHEN YOU CONTACT THE LUNAR SURFACE, YOUR DESCENT ENGINE"
320 PRINT"    WILL AUTOMATICALLY SHUT OFF AND YOU WILL BE GIVEN A"
330 PRINT"    REPORT OF YOUR LANDING SPEED AND REMAINING FUEL"
340 PRINT"(5) IF YOU RUN OUT OF FUEL, THE'?' WILL NO LONGER APPEAR"
350 PRINT"    BUT YOU SECOND BY SECOND REPORT WILL CONTINUE UNTIL"
360 PRINT"    YOU CONTACT LUNAR SURFACE.":PRINT
365 PRINT:INPUT "HIT <RETURN> TO CONTINUE...";W9
390 PRINT:PRINT:PRINT"BEGINING LANDING PROCEEDURE.........":PRINT
410 PRINT"G O O D  L U C K ! ! ! "
420 PRINT:PRINT
440 PRINT"SEC   FEET   SPEED   FUEL     PLOT OF DISTANCE"
450 PRINT
455 T=0:H=500:V=50:F=120
490 PRINT T;TAB(4);H;TAB(12);V;TAB(20);F;TAB(29);"I";TAB(H/12+29);"*"
500 INPUT B
510 IF B<0 THEN 650
520 IF B>30 THEN B=30
530 IF B>F THEN B=F
540 V1=V-B+5
560 F=F-B
570 H=H-.5*(V+V1)
580 IF H<=0 THEN 670
590 T=T+1
600 V=V1
610 IF F>0 THEN 490
615 IF B=0 THEN 640
620 PRINT:PRINT"***OUT OF FUEL***"
640 PRINT T;TAB(4);H;TAB(12);V;TAB(20);F;TAB(29);"I";TAB(H/12+29);"*"
650 B=0
660 GOTO 540
670 PRINT:PRINT"****CONTACT****"
680 H=H+.5*(V+V1)
690 IF B=5 THEN 720
700 D=(-V+SQR(V*V+H*(10-2*B)))/(5-B) 
710 GOTO 730
720 D=H/V
730 V1=V+(5-B)*D
760 PRINT"   TOUCHDOWN AT";T+D;"SECONDS."
770 PRINT"   LANDING VELOCITY =";V1;"FT/SEC"
780 PRINT F;"UNITS OF FUEL REMAINING."
790 IF V1<>0 THEN 810
800 PRINT:PRINT"CONGRADULATIONS!!!     A PERFECT LANDING!"
805 PRINT"YOUR LICENSE WILL BE RENEWED.........LATER."
808 GOTO 845
810 IF ABS(V1)<2 THEN 840
820 PRINT:PRINT" *****  SORRY, BUT YOU BLEW IT KIDO!!!!!"
825 PRINT" THIS WAS OUR ONLY CHANCE TO MAKE CONTACT WITH EXTRATRESTRIAL"
827 PRINT" BEINGS.  NON-APPROPRIATE CONDOLENCES MIGHT BE SENT TO YOUR"
830 PRINT" NEXT OF KIN."
835 GOTO 845
840 PRINT:PRINT"WELL......THAT WAS OK.  BUT NOT TOO SPECTACULAR..."
845 PRINT:PRINT
850 PRINT "ANOTHER MISSION ANYONE"
860 INPUT "(Type 1 if YES)";A
870 IF A=1 THEN 390
880 PRINT:PRINT  "CONTROL OUT. ":PRINT
999 END
"ANOTH