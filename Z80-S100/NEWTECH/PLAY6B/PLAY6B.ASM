; MICROPLAY REV.B DECEMBER 1977
; NEWTECH COMPUTER SYSTEMS INC.
; 230 Clinton Street
; Brooklyn, N.Y. 11201
;
; MICROPLAY STARTS AT THE BEGINHING OF THE
; MEMORY AREA DESIGNATED *SCORE* AND
; TRANSFERS INTO THE PLAY ROUTING A 1-BYTE
; PITCH PARAMETER AND A 2-BUTE DURATION
; PARAMETER.  (BITS A5,A4 AND A3 OF THE
; DURATION PARAMETER MOST SIGNIFICANT BYTE
; IS USED AS A FIELD TO SPECIFY WHICH
; AMPLITUDE ENVELOPE IS TO BE USED.)
; MICROPLAY CONTINUES TRANSFERRING NOTE
; PARAMETERS AND CALLING THE PLAY ROUTINE
; UNTIL A PITCH CONSTANT OF ZERO IS
; ENCOUNTERED WHICH INDICATES THE END OF
; THE MUSICAL SCORE.
;    THIS VERSION OF MICROPLAY WAS WRITTEN
; FOR A 8080 HAVING 0 WAIT STATES BUT CAN
; BE MODIFIED FOR 8080'S WITH WAIT STATES
; OR FOR Z80 PROCESSORS.
;
BEGIN		LD		SP,STACK		;INIT. STACK POINTER.
INIT		LD		HL,SCORE		;INIT. SCORE POINTER.
			LD		(PLACE),HL
NEXT		LD		HL,(PLACE)		;IF END OF SCORE THEN
			LD		A,0				;LOOP HERE.
			CP		(HL)
HERE		JP		Z, HERE			;YOUR ENDING?
;									;ELSE TRANSFER
;									;PARAMETERS FOR NEXT
;									;NOTE OF SCORE INTO
;									;PLAY ROUTINE.
			LD		A,(HL)			;LOAD PITCH.
			LD		(XFER2+1), A		
			LD		(XFER4+1), A
			INC		HL
			LD		A,(HL)			;LOAD NOTE DURATION
			LD		(XFER1+1), A	;LSD.
			LD		(XFER3+1), A
			INC		HL
			LD		A,(HL)			;LOAD NOTE DURATION
			AND		07H				;MASK 3 LSB'S.
			LD		(XFER1+2),A		;MSD.
			LD		(XFER3+2),A
			LD		A,(HL)			;GET MSD AGAIN
			AND		38H				;MASK 3 BITS
			ADD		A, 6FH			;ENVELOPE SPEC ADDRESS
			LD		(PLAY+1), A
			INC		HL
			LD		(PLACE), HL		;SAVE PLACE IN SCORE
;
			CALL	PLAY			;PLAY ONE NOTE.
			JP		NEXT			;GO DO NEXT NOTE.
;
;
;
PLAY		LD		HL,TBL1			;INIT EVELOPE POINTER
			LD		E,8				;INIT. SEGMENT COUNT
			LD		A,(HL)			;GET STARTING
;									;AMPLITUDE.
XFER1		LD		BC,LNGTH		;INIT. DURATION COUNT.
LOOP2		LD		B,B				;WASTE TIME (WT2)
			JP		XFER2			;(WT)
XFER2		LD		D,PITCH			;INIT. PITCH CONSTANT.
			OUT		(MODL6), A		;OUTPUT HALF WAVE TO
;									;MUSIC BOARD.
			INC		(HL)			;WASTE MUCH TIME (WMT)
			DEC		(HL)
			INC		(HL)			
			DEC		(HL)
			INC		(HL)			
			DEC		(HL)
LOOP3		DEC		D				;DELAY ACCORDING TO
			JP		NZ, LOOP3		;PITCH CONSTANT.
			XOR		(HL)			;COMPLEMENT A.
			DEC		C				;COUNT DOWN DURATION #.
			JP		NZ, LOOP2
			DEC		B
			JP		NZ, XFER2
			OUT		(MODL6), A
			INC		HL				;SET UP NEXT SEGMENT.
			DEC		E				;DCR SEGMENT COUNT.
			RET		Z				;RETURN IF ALL
;									;SEGMENTS DONE.
XFER3		LD		BC,LNGTH
XFER4		LD		D,PITCH
			LD		A,(HL)			;SET NEW AMPLITUDE.
			JP		LOOP3
; ENVELOPE SPECIFICATION:
; TBL1 THRU TBL8 ARE 8 DIFFERENT NOTE
; EVELOPE SPECIFICATIONS, EACH ONE
; CONSISTING OF 8 AMPLITUDE SEGMENTS.
;
TBL1		DB		0FFH,0FFH,0E0H,0C0H	;ATTACH:#5
			DB		0A0H,90H,70H,50H
TBL2		DB		0,0,0,0				;REST:#R
			DB		0,0,0,0
TBL3		DB		0C0H,0FFH,90H,60H	;STACCATO:#S
			DB		35H,0,0,0
TBL4		DB		0C0H,0FFH,0FFH,0FFH	;LEGATO:#L
			DB		0FFH,0FFH,0A0H,90H	
TBL5		DB		70H,80H,60H,20H		;SOFT STACCATO:#1
			DB		0,0,0,0				
TBL6		DB		70H,80H,80H,80H		;SOFT LEGATO:#2
			DB		80H,80H,60H,40H
TBL7		DB		0A0H,0B7H,0D0H,0FFH	;"SHAPED":#3
			DB		0FFH,0FFH,0C7H,0A0H	
TBL8		DB		70H,85H,0A0H,0B5H	;CRESCENDO:#4
			DB		0D0H,0FFH,0FFH,0A0H
;
PLACE		DW		0					;SCORE POINTER.
SCORE		EQU		0100H				;YOUR SCORE LOCATION?
MODL6		EQU		24H					;YOUR OUTPUT PORT?
PITCH		EQU		0					;DUMMY EQUATE
LNGTH		EQU		0					;DUMMY EQUATE
STACK		EQU		$+10H
			END