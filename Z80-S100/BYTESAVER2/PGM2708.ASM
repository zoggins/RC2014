;
;               2708 EPROM PROGRAMMER
;
;       THIS SAMPLE PROGRAM PROGRAMS TWO 2708 PROMS
;       IN SOCKETS ROM6 AND ROM7 ON CROMEMVO
;       BYTESAVER II MEMORY BOARD.  THE SOURCE CODE
;       IN THIS EXAMPLE IS ASSUMED LOCATED AT 200H - 9FFH.
;
SOURCE:		EQU		200H						;SOURCE CODE START ADDR
SWATH:		EQU		800H						;SOURCE CODE LENGTH
EPROM:		EQU		0F800H						;2708 PROM START ADDR
PGM2708:	LD		HL,SOURCE					;LOAD SOURCE ADDR,
			LD		DE,EPROM					;2708 START ADDR,
			LD		BC,SWATH					;AND SWATH FOR BLOCK XFER
			LDIR								;WRITE SOURCE TO 2708
			PUSH	HL							;SAVE LAST SOURCE+1 ADDR
			LD		HL,(PASSES)					;GET PROGRAMMING PASS#
			DEC		HL							;DECREMENT IT
			LD		A,H							;TEST FOR PASS#=0000H
			OR		L							;VALID ZERO FLAG
			LD		(PASSES),HL					;RESTORE PASS#
			POP		HL							;RESTORE LAST SOURCE+1
			JR		NZ,PGM2708					;PASS# NOT 0000H--GO BACK
			LD		BC,SWATH					;DONE--READY TO VERIFY
			DEC		HL							;POINT TO LAST SOURCE ADDR
VERIFY:		DEC		DE							;POINT TO LAST 2708 ADDR
			LD		A,(DE)						;GET 2708 BYTE AND
			CPD									;COMPARE TO SOURCE BYTE
			JP		PO,EXIT						;DONE IF (BC)=0000H
			JR		Z,VERIFY					;BYTES MATCH--NEXT BYTE
ERROR:		LD		A,0FFH						;MISMATCH--LOAD REG. A
			HALT								;WITH FFH AND HALT
EXIT:		LD		A,00H						;SUCCESS--LOAD REG. A
			HALT								;WITH 00H AND HALT
PASSES:		DW		360							;INITIAL PASS#=360
			END
